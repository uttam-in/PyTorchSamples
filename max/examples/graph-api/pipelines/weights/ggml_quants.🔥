# ===----------------------------------------------------------------------=== #
# Copyright (c) 2024, Modular Inc. All rights reserved.
#
# Licensed under the Apache License v2.0 with LLVM Exceptions:
# https://llvm.org/LICENSE.txt
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
# ===----------------------------------------------------------------------=== #
"""GGML quantized type definitions."""

from utils import InlineArray


@value
struct BlockQ40:
    """4-bit quantization.

    Constraints:
        The data layout must exactly match `block_q4_0` from ggml-quants.h.
    """

    alias QK4_0 = 32
    """Number of elements per Q4_0 block."""

    var d: Float16
    """Delta."""
    var qs: InlineArray[UInt8, Self.QK4_0 // 2]
    """Nibbles / quants."""

    def __init__(
        inout self,
        d: Float16,
        qs: InlineArray[UInt8, Self.QK4_0 // 2],
    ):
        constrained[sizeof[Self]() == sizeof[Float16]() + (Self.QK4_0 // 2)]()

        self.d = d
        self.qs = qs

    @staticmethod
    fn elements_per_block() -> Int:
        """Returns the number of elements per Q4_0 block."""
        return Self.QK4_0


@value
struct BlockQ80:
    """8-bit quantization.

    Constraints:
        The data layout must exactly match `block_q8_0` from ggml-quants.h.
    """

    alias QK8_0 = 32
    """Number of elements per Q8_0 block."""

    var d: Float16
    """Delta."""

    var qs: InlineArray[Int8, Self.QK8_0]
    """Quants."""

    def __init__(
        inout self,
        d: Float16,
        qs: InlineArray[Int8, Self.QK8_0],
    ):
        constrained[sizeof[Self]() == sizeof[Float16]() + Self.QK8_0]()

        self.d = d
        self.qs = qs

    @staticmethod
    fn elements_per_block() -> Int:
        """Returns the number of elements per Q8_0 block."""
        return Self.QK8_0


# Note that there is a compile definition in ggml-quants.h that allows setting
# `QK_K=64`, which is useful for models with rows unaligned to 256 bits.
alias QK_K = 256
"""Size of superblock quantized elements, in bytes."""

alias K_SCALE_SIZE = 12
"""Size of superblock scales and mins, in bytes."""


@value
struct BlockQ4K:
    """4-bit quantization.

    8 blocks of 32 elements each.
    Weights are represented as `x = a * q + b`.
    Effectively 4.5 bits per weight.

    Constraints:
        The data layout must exactly match `block_q4_K` from ggml-quants.h.
    """

    var d: Float16
    """Super-block scale for quantized scales."""

    var dmin: Float16
    """Super-block scale for quantized mins."""

    var scales: InlineArray[UInt8, K_SCALE_SIZE]
    """Scales and mins, quantized with 6 bits."""

    var qs: InlineArray[UInt8, QK_K // 2]
    """4-bit quants."""

    def __init__(
        inout self,
        d: Float16,
        dmin: Float16,
        scales: InlineArray[UInt8, K_SCALE_SIZE],
        qs: InlineArray[UInt8, QK_K // 2],
    ):
        constrained[
            sizeof[Self]() == 2 * sizeof[Float16]() + K_SCALE_SIZE + QK_K // 2
        ]()

        self.d = d
        self.dmin = dmin
        self.scales = scales
        self.qs = qs

    @staticmethod
    fn elements_per_block() -> Int:
        """Returns the number of elements per Q4_K block."""
        return QK_K


@value
struct BlockQ6K:
    """6-bit quantization.

    16 blocks of 16 elements each.
    Weights are represented as `x = a * q`.
    Effectively 6.5625 bits per weight.

    Constraints:
        The data layout must exactly match `block_q6_K` from ggml-quants.h.
    """

    var ql: InlineArray[UInt8, QK_K // 2]
    """Quants: lower 4 bits."""

    var qh: InlineArray[UInt8, QK_K // 4]
    """Quants: upper 2 bits."""
    var scales: InlineArray[Int8, QK_K // 16]
    """Scales: quantized with 8 bits."""

    var d: Float16
    """Super-block scale."""

    def __init__(
        inout self,
        ql: InlineArray[UInt8, QK_K // 2],
        qh: InlineArray[UInt8, QK_K // 4],
        scales: InlineArray[Int8, QK_K // 16],
        d: Float16,
    ):
        constrained[
            sizeof[Self]()
            == (QK_K // 2) + (QK_K // 4) + (QK_K // 16) + sizeof[Float16]()
        ]()

        self.ql = ql
        self.qh = qh
        self.scales = scales
        self.d = d

    @staticmethod
    fn elements_per_block() -> Int:
        """Returns the number of elements per Q6_K block."""
        return QK_K
